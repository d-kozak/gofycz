plugins {
  id 'base'
  id "com.moowork.node" version "1.2.0"
}

node {
   version = '10.14.1'
   npmVersion = '6.4.1'
   download = true
}


npm_run_build {
    inputs.files fileTree("public")
    inputs.files fileTree("src")
    inputs.file 'package.json'
    inputs.file 'package-lock.json'
    outputs.dir 'build'
}


task packageApp(type: Zip) {
    dependsOn npm_run_build
    baseName 'io.dkozak.gofycz.frontend'
    extension 'jar'
    destinationDir file("${projectDir}/jar")
    from('build') {
        // optional path under which output will be visible in Java classpath, e.g. static resources path
        into 'static'
    }
}

configurations {
    npmResources
}

configurations.default.extendsFrom(configurations.npmResources)

artifacts {
    npmResources(packageApp.archivePath) {
        builtBy packageApp
        type "jar"
    }
}

clean {
    delete packageApp.archivePath
}
